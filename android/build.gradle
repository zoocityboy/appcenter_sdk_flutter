group 'zoo.cityboy.appcenter'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.8.0'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 33

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    lintOptions {
      disable 'InvalidPackage'
      checkReleaseBuilds false
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        minSdkVersion 21
    }
    flavorDimensions "default"
    productFlavors {
        production {
            dimension "default"
        }
        staging {
            dimension "default"
        }
        development {
            isDefault true
            dimension "default"
        }
    }
}

dependencies {
    def appCenterSdkVersion = '5.0.2'
    implementation "com.microsoft.appcenter:appcenter-analytics:$appCenterSdkVersion"
    implementation "com.microsoft.appcenter:appcenter-crashes:$appCenterSdkVersion"
    developmentImplementation "com.microsoft.appcenter:appcenter-distribute:${appCenterSdkVersion}"
    stagingImplementation "com.microsoft.appcenter:appcenter-distribute:${appCenterSdkVersion}"
    productionImplementation "com.microsoft.appcenter:appcenter-distribute-play:${appCenterSdkVersion}"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'

    // if (System.env.FLUTTER_ROOT != null) {
    //     println "System property: " + System.getProperty("os.arch")
    //     println "FLUTTER_ROOT: $System.env.FLUTTER_ROOT"
    //     debugCompileOnly files(System.env.FLUTTER_ROOT + "/bin/cache/artifacts/engine/android-arm64/flutter.jar")
    // }
}
